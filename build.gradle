/*
 * Copyright 2021 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "java"
    id "java-library"
    id "com.google.protobuf" version "0.8.18"
    id "com.diffplug.spotless" version "5.9.0"
    id "idea"
    id "jacoco"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "maven-publish"
    id "com.google.cloud.artifactregistry.gradle-plugin" version "2.1.4"
}

repositories {
    mavenCentral()
    gradlePluginPortal()
    maven {
        url "https://packages.confluent.io/maven/"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

version = '1.0.0-SNAPSHOT'

def protoVersion = "3.20.1"
def beamVersion = "2.39.0"
def hadoopVersion = "3.3.1"
def autoValueVersion = "1.8.2"
def floggerVersion = "0.7.4"

dependencies {

    // Protobuf deps
    compileOnly "com.google.api.grpc:proto-google-common-protos:2.7.4"

    implementation "com.google.protobuf:protobuf-java:${protoVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protoVersion}"

    // Autovalue annotations
    compileOnly "com.google.auto.value:auto-value-annotations:${autoValueVersion}"
    annotationProcessor "com.google.auto.value:auto-value:${autoValueVersion}"
    testCompileOnly "com.google.auto.value:auto-value-annotations:${autoValueVersion}"
    testAnnotationProcessor "com.google.auto.value:auto-value:${autoValueVersion}"

    // Commons library deps
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation "org.apache.commons:commons-lang3:3.12.0"
    implementation "org.apache.httpcomponents:httpclient:4.5.13"

    // CSV Library
    implementation "org.apache.commons:commons-csv:1.9.0"

    // Google Services deps
    implementation "com.google.cloud:google-cloud-dlp:3.1.0"
    implementation "com.google.cloud:google-cloud-datacatalog:1.6.3"
    implementation "com.google.cloud:google-cloud-kms:2.5.1"
    implementation "com.google.cloud:google-cloud-secretmanager:2.0.7"
    implementation "com.squareup.okhttp3:okhttp:4.9.3"

    // Google Tink for encryption
    implementation 'com.google.crypto.tink:tink-android:HEAD-SNAPSHOT'
    implementation 'com.google.crypto.tink:tink-gcpkms:HEAD-SNAPSHOT'

    // JSON related libs
    implementation "com.github.wnameless.json:json-flattener:0.11.1"
    implementation "com.jayway.jsonpath:json-path:2.7.0"
    implementation "com.fasterxml.jackson.core:jackson-core:2.13.1"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.13.1"

    // Hadoop files (Avro + Parquet)
    implementation "org.apache.avro:avro:1.11.0"
    implementation "org.apache.parquet:parquet-avro:1.12.1"
    implementation("org.apache.hadoop:hadoop-common:${hadoopVersion}"){
        exclude group: "org.slf4j", module: "slf4j-log4j12"
    }

    // Logging framework
    implementation "com.google.flogger:flogger:${floggerVersion}"
    implementation "com.google.flogger:google-extensions:${floggerVersion}"
    runtimeOnly "com.google.flogger:flogger-system-backend:${floggerVersion}"

    // Apache Beam deps
    implementation "org.apache.beam:beam-sdks-java-core:${beamVersion}"
    implementation "org.apache.beam:beam-sdks-java-extensions-sorter:${beamVersion}"
    implementation "org.apache.beam:beam-sdks-java-io-jdbc:${beamVersion}"
    implementation "org.apache.beam:beam-sdks-java-io-google-cloud-platform:${beamVersion}"
    implementation "org.apache.beam:beam-sdks-java-io-parquet:${beamVersion}"
    testImplementation "org.apache.beam:beam-runners-direct-java:${beamVersion}"
    runtimeOnly "org.apache.beam:beam-runners-google-cloud-dataflow-java:${beamVersion}"

    // Runtime only deps
    runtimeOnly "org.slf4j:slf4j-api:1.7.36"
    runtimeOnly "org.slf4j:slf4j-jdk14:1.7.36"
    runtimeOnly "io.grpc:grpc-netty:1.44.0"

    runtimeOnly ("org.apache.hadoop:hadoop-mapreduce-client-core:${hadoopVersion}") {
        exclude group: "org.slf4j", module: "slf4j-log4j12"
    }
    testRuntimeOnly ("org.apache.hadoop:hadoop-mapreduce-client-core:${hadoopVersion}") {
        exclude group: "org.slf4j", module: "slf4j-log4j12"
    }

    // CloudSQL Connection Factory
    runtimeOnly "com.google.cloud.sql:mysql-socket-factory-connector-j-8:1.4.4"
    runtimeOnly "com.google.cloud.sql:postgres-socket-factory:1.4.4"


    // MySQL JDBC driver
    runtimeOnly('mysql:mysql-connector-java:8.0.25') {
        exclude group: "com.google.protobuf", module: "protobuf-java"
    }
    testRuntimeOnly('mysql:mysql-connector-java:8.0.25') {
        exclude group: "com.google.protobuf", module: "protobuf-java"
    }

    // Postgres SQL JDBC Driver
    runtimeOnly "org.postgresql:postgresql:42.3.2"

    //checker-framework
    compileOnly "org.checkerframework:checker-qual:3.21.2"

    // Testing Dependencies
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation "org.mockito:mockito-all:1.10.19"
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation "com.google.truth.extensions:truth-java8-extension:1.1.3"
    testImplementation "com.google.truth.extensions:truth-proto-extension:1.1.3"
    testImplementation "org.skyscreamer:jsonassert:1.5.0"
    testImplementation "org.apache.logging.log4j:log4j-core:2.17.0"

    // Testcontainers used to mock database
    testImplementation "org.testcontainers:mysql:1.16.3"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protoVersion}"
    }

    clean {
        delete protobuf.generatedFilesBaseDir
    }
}


java.sourceCompatibility = JavaVersion.VERSION_11
java.targetCompatibility = JavaVersion.VERSION_11

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(PublishToMavenRepository) {
    dependsOn(shadowJar)
}

shadowJar {
    zip64 = true
    mergeServiceFiles()
}

task pythonDist(type: Zip) {
    archiveFileName = "dapla-dlpflow.zip"
    destinationDirectory = file("$buildDir/libs")

    from "src/main/python"
}

test {
    dependsOn cleanTest
    testLogging.showStandardStreams = true
    finalizedBy jacocoTestReport
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    reports {
        csv.enabled false
        xml.enabled true
        html.enabled true
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "com/google/cloud/solutions/autotokenize/AutoTokenizeMessages*.class",
                    "**/*AutoValue_*.class"
            ])
        }))
    }
}

spotless {

    kotlin {
        target "**/*.kt"
        ktlint()
        trimTrailingWhitespace()
        indentWithSpaces(2)
        endWithNewline()
    }

    freshmark {
        target '*.md'
        indentWithSpaces(2)
        trimTrailingWhitespace()
        endWithNewline()
    }

    format "proto", {
        target "**/*.proto"
        licenseHeader("// Copyright \$YEAR Google LLC\n" +
                "//\n" +
                "// Licensed under the Apache License, Version 2.0 (the \"License\");\n" +
                "// you may not use this file except in compliance with the License.\n" +
                "// You may obtain a copy of the License at\n" +
                "//\n" +
                "//     http://www.apache.org/licenses/LICENSE-2.0\n" +
                "//\n" +
                "// Unless required by applicable law or agreed to in writing, software\n" +
                "// distributed under the License is distributed on an \"AS IS\" BASIS,\n" +
                "// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.\n" +
                "// See the License for the specific language governing permissions and\n" +
                "// limitations under the License.", "syntax")
        indentWithSpaces(2)
        trimTrailingWhitespace()
        endWithNewline()
    }


    format "gradle", {
        target "**/*.gradle"
        indentWithSpaces(2)
        trimTrailingWhitespace()
        endWithNewline()
    }

    format "misc", {
        target "**/.gitignore", "*.xml", "src/**/*.xml"
        indentWithSpaces(2)
        trimTrailingWhitespace()
        endWithNewline()
    }

}
